// This package defines lemmas for sets commonly used in specifications.
package sets

// TODO Add proper attribution
// SO Not taken from outside sources
// S1 https://github.com/dafny-lang/libraries/blob/master/src/Collections/Sets/Sets.dfy
// S2 https://github.com/verus-lang/verus/blob/main/source/pervasive/set_lib.rs
// S3 https://why3.lri.fr/stdlib/set.html
// S4 https://github.com/viperproject/silicon/blob/master/src/main/resources/dafny_axioms/sets.vpr


// TODO Restructure, rename and properly document definitions at the end
// (in particular consistency in names of functions, arguments (names/order), variables)

// QUES Should we use unicode comments like ∀ x,y : set[int]. |x| ≤ |y|?

// SO
// TODO Figure out how to import these from verify.gobra
type Unit struct{}

ghost
requires b
decreases
pure func Asserting(ghost b bool) Unit {
	return Unit{}
}

ghost
ensures false
decreases _
func TODO()


// S4
ghost
ensures (xs == ys) == (forall e int :: {e in xs} {e in ys} ((e in xs) == (e in ys)))
decreases
pure func SetEquality(xs, ys set[int]) Unit {
	return Unit{}
}

// S4
ghost
ensures e in (xs intersection ys) == ((e in xs) && (e in ys))
decreases
pure func InIntersectionInBoth(xs, ys set[int], e int) Unit {
	return Unit{}
}

// S4
ghost
ensures (e in (xs union ys)) == ((e in xs) || (e in ys))
decreases
pure func InUnionInOne(xs, ys set[int], e int) Unit {
	return Unit{}
}

// S4
// The length of a set is non-negative.
ghost
ensures len(x) >= 0
pure func LengthNonNegative(x set[int]) Unit {
	return Unit{}
}

// SO
// Returns whether x is a proper subset of y.
ghost
decreases
pure func IsProperSubset(x, y set[int]) bool {
	return x subset y && x != y
}

// S4
ghost
decreases
pure func AreDisjoint(xs, ys set[int]) bool {
	return IsEmpty(xs intersection ys)
}

// S1
ghost
decreases
pure func IsSingleton(x set[int]) bool {
	return len(x) == 1
}

// SO
// Returns a singleton set containing x.
ghost
ensures IsSingleton(result)
ensures x in result
decreases
pure func SingletonSet(x int) (result set[int]) {
	return set[int] {x}
}

// S1
// Elements in a singleton set are equal to each other
ghost
requires IsSingleton(x)
requires a in x
requires b in x
ensures a == b
decreases
pure func SingletonEquality(x set[int], a int, b int) Unit {
	return let _ := choose(x) in Unit{}
}

// S0
// If a is in a singleton set x, then x is of the form {a}
ghost
requires IsSingleton(x)
requires a in x
ensures x == SingletonSet(a)
pure func SingletonIsSingletonSet(x set[int], a int) Unit {
	return let _ := choose(x) in Unit{}
}

// S2
// Returns whether x is empty.
ghost
decreases
pure func IsEmpty(x set[int]) bool {
	return len(x) == 0
}

// S0
// Returns an empty set.
ghost
ensures IsEmpty(result)
decreases
pure func EmptySet() (result set[int]) {
	return set[int] {}
}

// S3, S4
// Nothing can be an element of the empty set.
ghost
requires IsEmpty(x)
ensures !(e in x)
decreases
pure func InEmpty(x set[int], e int) Unit {
	return Unit{}
}

// S3
// A set of length 0 is the empty set.
ghost
requires IsEmpty(x)
ensures x == EmptySet()
decreases
pure func EmptyIsEmptySet(x set[int]) Unit {
	return Unit{}
}

// S0/S1
// Returns an element from a non-empty set.
ghost
requires !IsEmpty(xs)
ensures x in xs
ensures len(xs) == 1 ==> xs == SingletonSet(x)
decreases
pure func choose(xs set[int]) (x int)

// TODO Get IsProperSubset case to verify
// S1
// If x is a subset of y, then the size of x is less than or equal to the size of y.
// If x is a strict subset of y, then the size of x is less than to the size of y.
// ghost
// decreases x, y
// ensures x subset y ==> len(x) <= len(y)
// ensures IsProperSubset(x, y) ==> len(x) < len(y)
// func SubsetSize(x, y set[int]) Unit {
	// if len(x) != 0 {
		// ghost e := choose(x)
		// SubsetSize(x setminus set[int] {e}, y setminus set[int] {e})
	// }
	// return Unit{}
// }

// S1
// If x is a subset of y and both have the same size, they are equal.
ghost
requires x subset y
requires len(x) == len(y)
ensures x == y
decreases
pure func SubsetEquality(x, y set[int]) Unit {
	return Asserting(len(y setminus x) == len(y) - len(x))
}

// S1
// A singleton set has a size of 1.
ghost
requires x == SingletonSet(e)
ensures len(x) == 1
decreases
pure func SingletonSize(x set[int], e int) Unit {
	return Unit{}
}


// TODO Prove this (Dafny Proof uses strict SubsetSize)
// S1
// Elements in a singleton set are equal to each other.
// ghost
// requires len(x) == 1
// requires a in x
// requires b in x
// ensures a == b
// pure func SingletonEquality(x set[int], a, b int) Unit {
// 	return Unit{}
// }

// S1
// The size of a union of two sets is greater than or equal to the size of
// either individual set.
ghost
ensures len(xs union ys) >= len(xs)
ensures len(xs union ys) >= len(ys)
decreases ys
pure func UnionSizeLower(xs, ys set[int]) Unit {
	return IsEmpty(ys) ? Unit{} :
		let y := choose(ys) in
		(let yr := Remove(ys, y) in
		(y in xs ?
			(let xr := Remove(xs, y) in
			(let _ := Asserting(xr union yr == Remove(xs union ys, y)) in UnionSizeLower(xr, yr))) :
			(let _ := Asserting(xs union yr == Remove(xs union ys, y)) in UnionSizeLower(xs, yr))))
}


// S1
// Construct a set with all integers in the range [a, b).
ghost
requires a <= b
ensures forall i int :: { i in s } (a <= i && i < b) == i in s
ensures len(s) == b - a
decreases b - a
pure func SetRange(a, b int) (s set[int]) {
	return a == b ? EmptySet() : Add(SetRange(a + 1, b), a)
}

// S1
// Construct a set with all integers in the range [0, n).
ghost
requires n >= 0
ensures forall i int :: {i in s} (0 <= i && i < n) == i in s
ensures len(s) == n
decreases
pure func SetRangeFromZero(n int) (s set[int]) {
	return SetRange(0, n)
}

// TODO Do we want S1: LemmaBoundedSetSize? (Dafny proof requires LemmaSubsetSize +
// contains unclear forall in proof)

// TODO: Do we even want to bother with multisets?
// S2
// Converts a set into a multiset where each element from the set has
// multiplicity 1 and any other element has multiplicity 0.
// ghost
// ensures forall i int :: {i # ms} (i in s) ==> ((i # ms) == 1)
// ensures forall i int :: {i # ms} (!(i in s)) ==> ((i # ms) == 0)
// decreases s
// pure func ToMultiset(s set[int]) (ms mset[int]) {
// 	return IsEmpty(s) ? mset[int] {} :
// 		let x := choose(s) in
// 		((mset[int] {}) union (mset[int] {x})) union ToMultiset(Remove(s, x))
// }

// S2
// The size of a union of two sets is less than or equal to the size of
// both individual sets combined.
ghost
ensures len(xs union ys) <= len(xs) + len(ys)
decreases
pure func UnionSizeUpper(xs, ys set[int]) Unit {
	return Unit{}
}

// S2
// The size of the intersection of xs and ys is less than or equal to the
// size of xs.
ghost
ensures len(xs intersection ys) <= len(xs)
decreases xs
pure func IntersectSizeUpper(xs, ys set[int]) Unit {
	return IsEmpty(xs) ? Unit{} :
		let x := choose(xs) in
		(let _ := Asserting((Remove(xs, x)) intersection ys == Remove((xs intersection ys), x)) in
		(IntersectSizeUpper(Remove(xs, x), ys)))

}


// S2
// The size of the difference xs - ys is less than or equal to the size of xs. 
ghost
ensures len(xs setminus ys) <= len(xs)
decreases
pure func SetminusSizeUpper(xs, ys set[int]) Unit {
	return Unit{}
}

ghost
ensures (xs union ys) == (ys union xs)
decreases
pure func UnionCommutativity(xs, ys set[int]) Unit {
	return Unit{}
}

ghost
ensures (xs intersection ys) == (ys intersection xs)
decreases
pure func IntersectionCommutativity(xs, ys set[int]) Unit {
	return Unit{}
}

// S2/S4
ghost
ensures (xs union ys) union ys == xs union ys
decreases
pure func UnionRightIdempotency(xs, ys set[int]) Unit {
	return Unit{}
}

// S2/S4
ghost
ensures xs union (xs union ys) == xs union ys
decreases
pure func UnionLeftIdempotency(xs, ys set[int]) Unit {
	return Unit{}
}

// S2
ghost
ensures (xs intersection ys) intersection ys == (xs intersection ys)
decreases
pure func IntersectionRightIdempotency(xs, ys set[int]) Unit {
	return Unit{}
}

// S2
ghost
ensures xs intersection (xs intersection ys) == (xs intersection ys)
decreases
pure func IntersectionLeftIdempotency(xs, ys set[int]) Unit {
	return Unit{}
}


// S4
ghost
ensures (e in (xs setminus ys)) == ((e in xs) && !(e in ys))
decreases
pure func InDifference(xs, ys set[int], e int) Unit {
	return Unit{}
}

// S2
// If ys contains e, then the difference xs - ys does not contain e.
ghost
requires e in ys
ensures !(e in (xs setminus ys))
decreases
pure func NotInDifference(xs, ys set[int], e int) Unit {
	return Unit{}
}

// S2
ghost
requires AreDisjoint(xs, ys)
ensures (xs union ys) setminus xs == ys
ensures (xs union ys) setminus ys == xs
decreases
pure func SetDisjoint(xs, ys set[int]) Unit {
	return Unit{}
}

ghost
requires AreDisjoint(xs, ys)
ensures len(xs union ys) == len(xs) + len(ys)
decreases
pure func DisjointUnionSize(xs, ys set[int]) Unit {
	return Unit{}
}

// S2 (proves slightly different version)
ghost
ensures len(xs union ys) == len(xs) + len(ys) - len(xs intersection ys)
decreases
pure func UnionSizeEq(xs, ys set[int]) Unit {
	return Unit{}
}

// S2
ghost
ensures len(xs setminus ys) == len(xs) - len(xs intersection ys)
ensures len(xs setminus ys) + len(ys setminus xs) + len(xs intersection ys) == len(xs union ys)
decreases
pure func SetminusSize(xs, ys set[int]) Unit {
	return Unit{}
}


// S3: Finite sets
// ***************
// S3
// Definition of subset without quantifiers.
ghost
requires e in xs
requires xs subset ys
ensures e in ys
decreases
pure func Subset(xs, ys set[int], e int) Unit {
	return Unit{}
}

// S3
// Subset relation is reflexive.
ghost
ensures xs subset xs
decreases
pure func SubsetReflexive(xs set[int]) Unit {
	return Unit{}
}

// S3
// Subset relation is transitive.
ghost
requires xs subset ys
requires ys subset zs
ensures xs subset zs
decreases
pure func SubsetTransitive(xs, ys, zs set[int]) Unit {
	return Unit{}
}

// S3 (add, add_def)
// Add an element x to the set xs.
ghost
// Need this post-condition to verify CardinalAdd
ensures (x in xs) ==> res == xs
ensures x in res
decreases
pure func Add(xs set[int], x int) (res set[int]) {
	return xs union SingletonSet(x)
}

// S4
ghost
ensures (e1 in Add(xs, e2)) == ((e1 == e2) || e1 in xs)
decreases
pure func InAddOther(xs set[int], e1, e2 int) Unit {
	return Unit{}
}

// S4
ghost
requires e1 in xs
ensures e1 in Add(xs, e2)
decreases
pure func InvarianceInAdd(xs set[int], e1, e2 int) Unit {
	return Unit{}
}

// S3
// Remove the element x from the set xs.
// TODO Should we add something like ensures !(x in xs) ==> xs == Remove(xs, x)
// like we did for Add?
ghost
decreases
pure func Remove(xs set[int], x int) set[int] {
	return xs setminus SingletonSet(x)
}

// S3
// If x is in the set xs, removing and adding it back in does not change the set
ghost
requires x in xs
ensures Add(Remove(xs, x), x) == xs
decreases
pure func AddRemove(xs set[int], x int) Unit {
	return Unit{}
}

// S3
// If we remove x from the set xs, it doesn't matter whether we have added x
// to it before.
ghost
ensures Remove(Add(xs, x), x) == Remove(xs, x)
decreases
pure func RemoveAdd(xs set[int], x int) Unit {
	return Unit{}
}

// S3
ghost
ensures Remove(xs, x) subset xs
decreases
pure func SubsetRemove(xs set[int], x int) Unit {
	return Unit{}
}


// TODO Should we merge SubsetUnion1 and SubsetUnion2?
// S3
ghost
ensures xs subset (xs union ys)
decreases
pure func SubsetUnion1(xs, ys set[int]) Unit {
	return Unit{}
}

// S3
ghost
ensures ys subset (xs union ys)
decreases
pure func SubsetUnion2(xs, ys set[int]) Unit {
	return Unit{}
}

// S3
ghost
ensures (xs intersection ys) subset xs
decreases
pure func SubsetIntersection1(xs, ys set[int]) Unit {
	return Unit{}
}

// S3
ghost
ensures (xs intersection ys) subset ys
decreases
pure func SubsetIntersection2(xs, ys set[int]) Unit {
	return Unit{}
}

// S3
ghost
ensures (xs setminus ys) subset xs 
decreases
pure func SubsetSetminus(xs, ys set[int]) Unit {
	return Unit{}
}


// S3
ghost
ensures len(xs) >= 0
decreases
pure func CardinalNonneg(xs set[int]) Unit {
	return Unit{}
}


// S3
ghost
ensures (x in xs) ==> (len(Add(xs, x)) == len(xs))
ensures !(x in xs) ==> (len(Add(xs, x)) == len(xs) + 1)
decreases
func CardinalAdd(xs set[int], x int) Unit {
	return Unit{}
}

// S3
ghost
ensures (x in xs) ==> (len(Remove(xs, x)) == len(xs) - 1)
ensures !(x in xs) ==> (len(Remove(xs, x)) == len(xs))
decreases
func CardinalRemove(xs set[int], x int) Unit {
	return Unit{}
}
// S3
// If xs is a Singleton, and x is in xs, then choose(xs) will always return x.
ghost
requires len(xs) == 1
ensures x in xs ==> choose(xs) == x
decreases
pure func Cardinal1(xs set[int], x int) Unit {
	return Unit{}
}

// TODO S3: min_elt{,_def}, max_elt{,_def} mention forall x . x in xs in
// their post-condition; do we want these functions?


// S3 The sections in "Finite Monomorphic sets" suggest to move cardinality properties as a
// postcondition to functions that "generate" it
// Empty() would return an empty list, but also ensure that its cardinality is 0
// Add(xs, x) would return a set where x has been added to xs, but also ensure that
// the cardinality stays the same if x was already in xs, or increases if it wasnt
// A similar consideration is made for Singleton(x) and Remove(xs, x)
// TODO Do we want to merge functions like this?
