/*
  This file is part of gobra-libs which is released under the MIT license.
  See LICENSE or go to https://github.com/viperproject/gobra-libs/blob/main/LICENSE
  for full license details.

  This file is inspired by the following content:
  - dafny-lang/libraries: https://github.com/dafny-lang/libraries/blob/master/src/Collections/Sets/Sets.dfy
  - verus-lang/verus: https://github.com/verus-lang/verus/blob/main/source/pervasive/set_lib.rs
  - why3: https://why3.lri.fr/stdlib/set.html
  - viperproject/silicon: https://github.com/viperproject/silicon/blob/master/src/main/resources/dafny_axioms/sets.vpr

*/

// This package defines lemmas for sets commonly used in specifications.
package sets

// ##(-I ../)
import utils "utils"

// QUES Do we want to have comments for every function? In some cases it is trivial, in other
// cases it feels like the natural language version is less understandable than the formal specs.
// TODO Improve comments

// Empty set
// *********
// A set is empty if it has cardinality 0.
ghost
decreases
pure func IsEmpty(xs set[int]) bool {
	return len(xs) == 0
}

// Returns an empty set.
ghost
ensures IsEmpty(result)
decreases
pure func EmptySet() (result set[int]) {
	return set[int]{}
}

// There is only one empty set.
ghost
requires IsEmpty(xs)
ensures xs == EmptySet()
decreases
pure func EmptySetIsUnique(xs set[int]) utils.Unit {
	return utils.Unit{}
}

// An empty set doesn't have any elements.
ghost
requires IsEmpty(xs)
ensures !(e in xs)
decreases
pure func NotInEmpty(xs set[int], e int) utils.Unit {
	return utils.Unit{}
}

// Singleton set
// **************
// A set is a singleton if it has cardinality 1.
ghost
decreases
pure func IsSingleton(xs set[int]) bool {
	return len(xs) == 1
}

// Returns a singleton containing x.
ghost
ensures IsSingleton(result)
ensures e in result
decreases
pure func SingletonSet(e int) (result set[int]) {
	return set[int]{e}
}

// If a is in a singleton set x, then x is of the form {a}
ghost
requires IsSingleton(xs)
requires e in xs
ensures xs == SingletonSet(e)
decreases
pure func SingletonIsSingletonSet(xs set[int], e int) utils.Unit {
	return let _ := choose(xs) in utils.Unit{}
}

// Elements in a singleton set are equal to each other.
ghost
requires IsSingleton(xs)
requires a in xs
requires b in xs
ensures a == b
decreases
pure func SingletonEquality(xs set[int], a int, b int) utils.Unit {
	return let _ := choose(xs) in utils.Unit{}
}

// Constructing new sets
// **********************
// Construct a set with all integers in the range [a, b).
ghost
requires a <= b
ensures forall i int :: { i in result } (a <= i && i < b) == i in result
ensures len(result) == b - a
decreases b - a
pure func Range(a, b int) (result set[int]) {
	return a == b ? EmptySet() : Add(Range(a + 1, b), a)
}

// Construct a set with all integers in the range [0, n).
ghost
requires n >= 0
ensures forall i int :: { i in result } (0 <= i && i < n) == i in result
ensures len(result) == n
decreases
pure func RangeFromZero(n int) (result set[int]) {
	return Range(0, n)
}

// Converts a set into a multiset where each element from the set has
// multiplicity 1 and any other element has multiplicity 0.
ghost
ensures forall i int :: {i # ms} (i in s) ==> ((i # ms) == 1)
ensures forall i int :: {i # ms} (!(i in s)) ==> ((i # ms) == 0)
decreases s
pure func ToMultiset(s set[int]) (ms mset[int]) {
	return IsEmpty(s) ? mset[int] {} :
		let x := choose(s) in
		((mset[int] {}) union (mset[int] {x})) union ToMultiset(Remove(s, x))
}


// choose axiom
// ************
// Returns an element from a non-empty set.
ghost
requires !IsEmpty(xs)
ensures e in xs
ensures IsSingleton(xs) ==> xs == SingletonSet(e)
decreases
pure func choose(xs set[int]) (e int)

// General definitions and properties
// **********************************
// Returns whether xs and ys are disjoint sets.
ghost
decreases
pure func AreDisjoint(xs, ys set[int]) bool {
	return IsEmpty(xs intersection ys)
}

// Definition of set equality.
ghost
ensures (xs == ys) == (forall e int :: {e in xs} {e in ys} ((e in xs) == (e in ys)))
decreases
pure func SetEquality(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

// Subset
// ******
// Definition of subset without quantifiers.
ghost
requires e in xs
requires xs subset ys
ensures e in ys
decreases
pure func InSubset(xs, ys set[int], e int) utils.Unit {
	return utils.Unit{}
}

// Subset relation is reflexive.
ghost
ensures xs subset xs
decreases
pure func SubsetIsReflexive(xs set[int]) utils.Unit {
	return utils.Unit{}
}

// Subset relation is transitive.
ghost
requires xs subset ys
requires ys subset zs
ensures xs subset zs
decreases
pure func SubsetIsTransitive(xs, ys, zs set[int]) utils.Unit {
	return utils.Unit{}
}

// If x is a subset of y and both have the same cardinality, they are equal.
ghost
requires xs subset ys
requires len(xs) == len(ys)
ensures xs == ys
decreases
pure func SubsetEquality(xs, ys set[int]) utils.Unit {
	return utils.Asserting(len(ys setminus xs) == len(ys) - len(xs))
}

// Returns whether x is a proper subset of y.
ghost
decreases
pure func IsProperSubset(xs, ys set[int]) bool {
	return xs subset ys && xs != ys
}

// Union
// *****
// If an e is in the union of xs and ys, then it must be in xs or ys.
ghost
ensures (e in (xs union ys)) == ((e in xs) || (e in ys))
decreases
pure func InUnionInOne(xs, ys set[int], e int) utils.Unit {
	return utils.Unit{}
}

// Union is commutative.
ghost
ensures (xs union ys) == (ys union xs)
decreases
pure func UnionIsCommutative(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures (xs union ys) union ys == xs union ys
decreases
pure func UnionIsRightIdempotent(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures xs union (xs union ys) == xs union ys
decreases
pure func UnionIsLeftIdempotent(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

// Add (union with singleton)
// **************************
// Add an element x to the set xs.
ghost
// Need this post-condition to verify CardinalAdd
ensures (e in xs) ==> res == xs
ensures e in res
decreases
pure func Add(xs set[int], e int) (res set[int]) {
	return xs union SingletonSet(e)
}

ghost
ensures (a in Add(xs, b)) == ((a == b) || a in xs)
decreases
pure func InAdd(xs set[int], a, b int) utils.Unit {
	return utils.Unit{}
}

ghost
requires a in xs
ensures a in Add(xs, b)
decreases
pure func InvarianceInAdd(xs set[int], a, b int) utils.Unit {
	return utils.Unit{}
}

// Remove (setminus with singleton)
// ********************************
// Remove the element x from the set xs.
// QUES Should we add something like ensures !(x in xs) ==> xs == Remove(xs, x)
// like we did for Add?
ghost
decreases
pure func Remove(xs set[int], e int) set[int] {
	return xs setminus SingletonSet(e)
}

// Intersection
// ************
ghost
ensures (xs intersection ys) == (ys intersection xs)
decreases
pure func IntersectionIsCommutative(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures (xs intersection ys) intersection ys == (xs intersection ys)
decreases
pure func IntersectionIsRightIdempotent(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures xs intersection (xs intersection ys) == (xs intersection ys)
decreases
pure func IntersectionIsLeftIdempotent(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}


// Setminus
// ********
ghost
ensures (e in (xs setminus ys)) == ((e in xs) && !(e in ys))
decreases
pure func InDifference(xs, ys set[int], e int) utils.Unit {
	return utils.Unit{}
}

// If ys contains e, then the difference xs - ys does not contain e.
ghost
requires e in ys
ensures !(e in (xs setminus ys))
decreases
pure func NotInDifference(xs, ys set[int], e int) utils.Unit {
	return utils.Unit{}
}

// Relating multiple operations
// ****************************
ghost
ensures e in (xs intersection ys) == ((e in xs) && (e in ys))
decreases
pure func InIntersectionInBoth(xs, ys set[int], e int) utils.Unit {
	return utils.Unit{}
}

ghost
requires AreDisjoint(xs, ys)
ensures (xs union ys) setminus xs == ys
ensures (xs union ys) setminus ys == xs
decreases
pure func DisjointUnionDifference(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

// If e is in xs, removing and adding it back yields the original set.
ghost
requires e in xs
ensures Add(Remove(xs, e), e) == xs
decreases
pure func AddRemove(xs set[int], e int) utils.Unit {
	return utils.Unit{}
}

// If we remove x from the set xs, it doesn't matter whether we have added x
// to it before.
ghost
ensures Remove(Add(xs, e), e) == Remove(xs, e)
decreases
pure func RemoveAdd(xs set[int], e int) utils.Unit {
	return utils.Unit{}
}

ghost
ensures Remove(xs, e) subset xs
decreases
pure func SubsetRemove(xs set[int], e int) utils.Unit {
	return utils.Unit{}
}

// QUES Should we merge SubsetUnion1 and SubsetUnion2?
ghost
ensures xs subset (xs union ys)
decreases
pure func SubsetUnion1(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures ys subset (xs union ys)
decreases
pure func SubsetUnion2(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures (xs intersection ys) subset xs
decreases
pure func SubsetIntersection1(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures (xs intersection ys) subset ys
decreases
pure func SubsetIntersection2(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures (xs setminus ys) subset xs 
decreases
pure func SubsetDifference(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}


// Cardinality
// ***********
// The cardinality of a set is non-negative.
ghost
ensures len(xs) >= 0
decreases
pure func NonNegativeLen(xs set[int]) utils.Unit {
	return utils.Unit{}
}

// QUES I cannot get the IsProperSubset case to verify; Verus doesn't include this case either. Do we skip it for now?
// If x is a subset of y, then the cardinality of x is less than or equal to the cardinality of y.
// {If x is a strict subset of y, then the cardinality of x is less than to the cardinality of y.}
// ghost
// decreases x, y
// ensures x subset y ==> len(x) <= len(y)
// ensures IsProperSubset(x, y) ==> len(x) < len(y)
// func SubsetLen(x, y set[int]) utils.Unit {
// 	if len(x) != 0 {
// 		ghost e := choose(x)
// 		SubsetLen(x setminus set[int] {e}, y setminus set[int] {e})
// 	}
// 	return utils.Unit{}
// }

// The cardinality of a union of two sets is greater than or equal to the cardinality of
// either individual set.
ghost
ensures len(xs union ys) >= len(xs)
ensures len(xs union ys) >= len(ys)
decreases ys
pure func UnionLenLower(xs, ys set[int]) utils.Unit {
	return IsEmpty(ys) ? utils.Unit{} :
		let y := choose(ys) in
		(let yr := Remove(ys, y) in
		(y in xs ?
			(let xr := Remove(xs, y) in
			(let _ := utils.Asserting(xr union yr == Remove(xs union ys, y)) in UnionLenLower(xr, yr))) :
			(let _ := utils.Asserting(xs union yr == Remove(xs union ys, y)) in UnionLenLower(xs, yr))))
}

// The cardinality of a union of two sets is less than or equal to the cardinality of
// both individual sets combined.
ghost
ensures len(xs union ys) <= len(xs) + len(ys)
decreases
pure func UnionLenUpper(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

// The cardinality of the intersection of xs and ys is less than or equal to the
// cardinality of xs.
ghost
ensures len(xs intersection ys) <= len(xs)
decreases xs
pure func IntersectLenUpper(xs, ys set[int]) utils.Unit {
	return IsEmpty(xs) ? utils.Unit{} :
		let x := choose(xs) in
		(let _ := utils.Asserting((Remove(xs, x)) intersection ys == Remove((xs intersection ys), x)) in
		(IntersectLenUpper(Remove(xs, x), ys)))
}

// The cardinality of the difference xs - ys is less than or equal to the cardinality of xs. 
ghost
ensures len(xs setminus ys) <= len(xs)
decreases
pure func DifferenceLenUpper(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures len(xs union ys) == len(xs) + len(ys) - len(xs intersection ys)
decreases
pure func UnionLenEq(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures len(xs setminus ys) == len(xs) - len(xs intersection ys)
ensures len(xs setminus ys) + len(ys setminus xs) + len(xs intersection ys) == len(xs union ys)
decreases
pure func DifferenceLenEq(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

ghost
ensures (e in xs) ==> (len(Add(xs, e)) == len(xs))
ensures !(e in xs) ==> (len(Add(xs, e)) == len(xs) + 1)
decreases
func AddLen(xs set[int], e int) utils.Unit {
	return utils.Unit{}
}

ghost
ensures (e in xs) ==> (len(Remove(xs, e)) == len(xs) - 1)
ensures !(e in xs) ==> (len(Remove(xs, e)) == len(xs))
decreases
func RemoveLen(xs set[int], e int) utils.Unit {
	return utils.Unit{}
}

ghost
requires AreDisjoint(xs, ys)
ensures len(xs union ys) == len(xs) + len(ys)
decreases
pure func DisjointUnionLen(xs, ys set[int]) utils.Unit {
	return utils.Unit{}
}

// QUES Do we want S1: LemmaBoundedSetSize? (Dafny proof requires LemmaSubsetSize +
// contains unclear forall in proof)
// TODO If we do want this; make sure to call it something like BoundedSetLen to be consistent.
// Dafny signature:
//   lemma LemmaBoundedSetSize(x: set<int>, a: int, b: int)
//     requires forall i {:trigger i in x} :: i in x ==> a <= i < b
//     requires a <= b
//     ensures |x| <= b - a

// *****************

// QUES S3: min_elt{,_def}, max_elt{,_def} mention forall x . x in xs in
// their post-condition; do we want these functions?


// S3 The sections in "Finite Monomorphic sets" suggest to move cardinality properties as a
// postcondition to functions that "generate" it
// Empty() would return an empty list, but also ensure that its cardinality is 0
// Add(xs, x) would return a set where x has been added to xs, but also ensure that
// the cardinality stays the same if x was already in xs, or increases if it wasnt
// A similar consideration is made for Singleton(x) and Remove(xs, x)
// QUES Do we want to merge functions like this?
